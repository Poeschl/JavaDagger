group 'io.github.poeschl.example'
version '1.0'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.6"
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3"
    }
}

apply plugin: 'java'
apply plugin: "net.ltgt.apt"
apply plugin: 'idea'
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "jacoco"
apply plugin: 'com.github.kt3k.coveralls'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

//noinspection GroovyAssignabilityCheck
jar {
    manifest {
        attributes 'Main-Class': 'io.github.poeschl.example.javadagger.CoffeeApp'
    }
}

shadowJar {
    classifier = null
}

jacoco {
    toolVersion = "0.7.6.201602180812"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ["**/Dagger*.*", "**/*Component*", "**/*Module.*", "**/*_Factory.*", "**/*_Provide*Factory.*", "**/CoffeeApp.*"])
        })
    }
}

dependencies {
    apt 'com.google.dagger:dagger-compiler:2.5'
    compile 'com.google.dagger:dagger:2.5'

    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-all:1.8.4'
}
